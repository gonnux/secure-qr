plugins {
    id 'com.android.application'
}

android {
    namespace 'com.gonnux.secureqr'
    compileSdk 35

    signingConfigs {
        release {
            storeFile file(project.findProperty("KEYSTORE_PATH") ?: "/dev/null")
            storePassword project.findProperty("KEYSTORE_PASSWORD")
            keyAlias project.findProperty("KEY_ALIAS")
            keyPassword project.findProperty("KEY_PASSWORD")
        }
    }

    defaultConfig {
        applicationId "com.gonnux.secureqr"
        minSdk 26
        targetSdk 35
        versionCode 7
        versionName "1.0.5"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
            // R8 코드 축소 활성화 - deobfuscation mapping 파일 자동 생성
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    // 네이티브 디버그 심볼을 번들에 포함
    buildTypes.each {
        it.ndk.debugSymbolLevel = 'SYMBOL_TABLE'
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    buildFeatures {
        viewBinding true
    }
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'com.google.android.material:material:1.10.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'androidx.lifecycle:lifecycle-livedata-ktx:2.6.2'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.6.2'
    implementation 'androidx.navigation:navigation-fragment:2.7.5'
    implementation 'androidx.navigation:navigation-ui:2.7.5'
    testImplementation 'junit:junit:4.+'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
    // CameraX
    def camerax_version = "1.3.4"
    implementation "androidx.camera:camera-core:${camerax_version}"
    implementation "androidx.camera:camera-camera2:${camerax_version}"
    implementation "androidx.camera:camera-lifecycle:${camerax_version}"
    implementation "androidx.camera:camera-view:${camerax_version}"
    implementation "androidx.camera:camera-extensions:${camerax_version}"
    implementation 'com.google.mlkit:barcode-scanning:17.3.0'
    implementation 'io.github.ParkSangGwon:tedpermission-normal:3.3.0'
    implementation 'com.journeyapps:zxing-android-embedded:4.3.0'
    implementation 'com.google.android.gms:play-services-ads:20.4.0'

    // For apps targeting Android 12, add WorkManager dependency.
    constraints {
        implementation('androidx.work:work-runtime:2.7.0') {
            because '''androidx.work:work-runtime:2.1.0 pulled from play-services-ads
                   has a bug using PendingIntent without FLAG_IMMUTABLE or
                   FLAG_MUTABLE and will fail in apps targeting S+.'''
        }
    }
}
